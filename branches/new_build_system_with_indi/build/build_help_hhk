#!/usr/bin/perl

# script to produce PHD2GuideHelp.hhk by reading the list of html
# files from PHD2GuideHelp.hhp and locating all the HTML anchors

use strict;

my %a;

print "reading PHD2GuideHelp.hhp\n";

open HHP, "<PHD2GuideHelp.hhp";
my $files;
while (<HHP>) {
    chomp;
    s/^\s+//g;
    s/\s+$//g;
    next unless length $_;
    if ($files) {
        last if /^\[/;
        my $f = $_;
        print "  $f\n";
        open F, "<$f" or die "could not open $f: $!\n";
        while (<F>) {
            foreach (m/<\s*a\s+name\s*=\s*"([^\"]+)"/ig,
                     m/<\s*a\s+name\s*=\s*(\w+)\s*[^>]*>/ig) {
                my $text = ucfirst $_;
                $text =~ s/_/ /g;
                $a{lc $text} = [ $_, $f, $text ];
            }
        }
    }
    elsif (/^\[FILES\]/) {
        $files = 1;
    }
}

print "writing PHD2GuideHelp.hhk\n";

open OUT, ">PHD2GuideHelp.hhk";

print OUT <<'EOF';
<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<HTML><HEAD>
<!-- Sitemap 1.0 -->
</HEAD><BODY>
<UL>
EOF

foreach (sort keys %a) {
    my ($a, $f, $text) = @{$a{$_}};
    print OUT <<EOF;
\t<LI><OBJECT type="text/sitemap">
\t\t<param name="Name" value="$text">
\t\t<param name="Name" value="$a">
\t\t<param name="Local" value="$f#$a">
\t\t</OBJECT>
EOF
}

print OUT <<'EOF';
</UL>
</BODY></HTML>
EOF

print "done.\n";
